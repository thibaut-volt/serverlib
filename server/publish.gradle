apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.volt'
def LIB_ARTIFACT_ID = 'server-android'
def LIB_VERSION = '0.0.2'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

def getLocalPropertyOrEnvVar(def propertyName) {
    File propertiesFile = rootProject.file('local.properties')
    Properties properties = new Properties()

    logger.lifecycle('getLocalPropertyOrEnvVar.')

    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
        if (properties.getProperty(propertyName) != null) {
            logger.lifecycle("getLocalPropertyOrEnvVar. name ${propertyName} = ${properties.getProperty(propertyName)}")
            return properties.getProperty(propertyName)
        } else {
            return System.getenv(propertyName).toString()
        }
    } else if (System.hasProperty(propertyName)) {
        return System.getenv(propertyName).toString()
    } else {
        return ""
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/thibaut-volt/serverlib")
            credentials {
                username = getLocalPropertyOrEnvVar("gpr.user")
                password = getLocalPropertyOrEnvVar("gpr.key")
            }
        }
        maven {
            name = 'CustomMavenRepo'
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        server(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/server-release.aar")
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}
